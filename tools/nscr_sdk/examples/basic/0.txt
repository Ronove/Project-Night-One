;ご要望の機能を追加しておきました。
;チェックお願いします。
;
;【メニューのカスタマイズ】
;
;次のサンプルを見てください。


*define
;resetmenuでメニューをカスタマイズすることを宣言
resetmenu
;以後、insertmenuでメニューを挿入していく。常に先頭に挿入される。
;insertmenu "テキスト",機能コード(,サブメニュー階層数)
;最後に,数字がある場合、それはその数だけ掘ったサブメニューへの挿入を意味する。
;ちょっと文法がややこしいので、わからなかったら、こういうメニューが欲しい、
;というのをメールでください。実現します。

insertmenu "終了",END
insertmenu "バージョン情報",VERSION
insertmenu "次の選択肢に進む",SKIP
insertmenu "オートモード",AUTO
insertmenu "環境設定",SUB
;SUBはサブメニューが開くことをあらわします。
insertmenu "フォント",FONT,1
insertmenu "ウェーブ",SUB,1
insertmenu "ＯＮ",WAVEON,2
insertmenu "ＯＦＦ",WAVEOFF,2
insertmenu "ボリューム",DWAVEVOLUME,2
insertmenu "テキスト",SUB,1
insertmenu "遅い",TEXTSLOW,2
insertmenu "普通",TEXTMIDDLE,2
insertmenu "速い",TEXTFAST,2
insertmenu "画面",SUB,1
insertmenu "フルスクリーン",FULL,2
insertmenu "ウィンドウ",WINDOW,2
insertmenu "ＣＤ−ＤＡ",SUB,1
insertmenu "ＯＮ",CDON,2
insertmenu "ＯＦＦ",CDOFF,2
insertmenu "クリック設定",SUB,1
insertmenu "普通",CLICKDEF,2
insertmenu "ページごと",CLICKPAGE,2

game

*start

;複合ボタンの使い方。
;exbtn スプライト番号,ボタン番号,制御文字列
;最後に制御文字列が付く以外はspbtnと同じです。機能も同等。
;ただし、そのボタンにカーソルが重なったとき、ボタンを変化させるだけでなく、
;制御文字列で指定されたスプライト制御を実行します。

;制御文字列は次の形式で書いてください。
;"C数字" 指定したスプライト番号を非表示にする 
;"P数字" 指定したスプライトを表示する
;"P数字1,数字2" 数字1のスプライトのセル数字2を表示する

;制御文字列では、上の三つをいくつでも連結して指定できます。
;例）
;"C10"
;"C11P10"
;"C11P10,2P9"

;複合ボタンでは、「どれにもカーソルが重なっていないとき」の指定が必要です。
;それをexbtn_dで実現します。

;ボタンは他のものと同様、文字列スプライトでも普通の画像スプライトでもＯＫです。

lsp 0,":s#888888#FFFFFFボタン１番",50,50
lsp 1,":s#888888#FFFFFFボタン２番",50,100
lsp 2,":s#888888#FFFFFFボタン３番",50,150

lsph 3,":s#FFFFFFどれにも重なっていない",200,300
lsph 4,":s#FFFFFFボタン１番",200,300
lsph 5,":s#FFFFFFボタン２番",200,300
lsph 6,":s#FFFFFFボタン３番",200,300

lsp 7,":c/4,0,3;number.bmp",50,300


btndef ""
exbtn_d "P3C4C5C6P7,0"
exbtn 0,1,"C3P4C5C6P7,1"
exbtn 1,2,"C3C4P5C6P7,2"
exbtn 2,3,"C3C4C5P6P7,3"
btnwait %0
csp -1
print 10,1000
ボタン命令は%0を返しました。@
end

;この複合ボタン用の文字列でのスプライト指定が便利なので、
;単独でその機能だけ実行する命令もつけました。
;spstr 文字列 で実行できます。
;（ただし、cspやlspやvspと同じく後でprintが必要）
;例）spstr "C11P10,2P9"